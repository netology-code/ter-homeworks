# Домашнее задание к занятию «Основы Terraform. Yandex Cloud»

### Задание 1

Сервисный аккаунт и ключ созданы

<img width="1358" height="438" alt="image" src="https://github.com/user-attachments/assets/06b3dd0a-6239-46b3-8175-d9bcd0e0192a" />

SSH-ключ есть, переменная `vms_ssh_public_root_key` записана в `personal.auto.tfvars`

<img width="1632" height="145" alt="image" src="https://github.com/user-attachments/assets/7cb58ca1-af20-487f-9336-c51c94fc734d" />

Проект инициализируется, `terraform plan` завершается предупреждениями:
```
Value for undeclared variable
The root module does not declare a variable named "service_account_key_file"
The root module does not declare a variable named "vms_ssh_public_root_key"
```
То есть в personal.auto.tfvars указаны эти переменные, но в коде `variables.tf`` нет блоков variable с этими именами. Нужно добавить их:

Возникает ошибка:

<img width="1930" height="312" alt="image" src="https://github.com/user-attachments/assets/6fabef09-f35b-4a05-b309-f1fc47464713" />

Потому что service_account_key_file задан явно, а не переменной: `service_account_key_file = file("~/.authorized_key.json")`. Надо сделать: `var.service_account_key_file`.

<img width="1663" height="202" alt="image" src="https://github.com/user-attachments/assets/415adc5b-a632-4bb0-8c58-a7317143beb4" />

Возникают ошибки
"Platform "standart-v4" not found" - потому чти типа ВМ "standart-v4" в яндексе нет, нужно исправить main.tf и указать в ресурсе **platform_id = "standard-v3"** (т.е. Intel Ice Lake)
"the specified core fraction is not available" - потому что минимальная доля ЦПУ для ВМ 20%. Нужно указать `core_fraction = 20`.
"the specified number of cores is not available" - потому что минимальное количество ядер для standard-v3 - 2

Ошибки в resource исправлены:
<img width="1596" height="224" alt="image" src="https://github.com/user-attachments/assets/0bd3c66e-3998-4c52-b15b-d9310aa1fa32" />

Также в main.tf указано `ssh-keys = "ubuntu:${var.vms_ssh_root_key}"`, исправлено на `${var.vms_ssh_public_root_key}"`, чтобы ключ корректно подкидывался в создаваемую ВМ.


После исправления -код исполняется, ресурс создан:

<img width="2293" height="469" alt="image" src="https://github.com/user-attachments/assets/8fbfb7e9-021b-4e9e-b8a4-7c9f0734b5f3" />


Посл подключения по SSH:

<img width="852" height="72" alt="image" src="https://github.com/user-attachments/assets/701367d3-0ca7-41d3-8c80-3395b1c0d388" />

По поводу `preemptible = true` и `core_fraction=5` объяснялось на вебинаре.
- preemptible это прерываемая ВМ с ограниченным временем работы (до 24 часов), может быть автоматически остановлена в любой момент для высвобожения ресурсов. 
- core fraction ограничивает гарантированную долю процессорного времени, которое доступно ВМ. Полезно для легких задач, не требующих значительной вычислительной мощности.
Оба параметра позволяют существенно экономить средства и запускать легковесные ВМ для экспериментов при учёбе.


### Задание 2

1. Замените все хардкод-**значения** для ресурсов **yandex_compute_image** и **yandex_compute_instance** на **отдельные** переменные. К названиям переменных ВМ добавьте в начало префикс **vm_web_** .  Пример: **vm_web_name**.
2. Объявите нужные переменные в файле variables.tf, обязательно указывайте тип переменной. Заполните их **default** прежними значениями из main.tf. 
3. Проверьте terraform plan. Изменений быть не должно.

Хардкод-значения в main.tf такие:
- name = "netology-develop-platform-web" - будет переменная vm_web_name
- platform_id = "standard-v3" - будет переменная vm_web_platform_id
- cores = 2 - будет переменная vm_web_cores
- memory = 1 - будет переменная vm_web_memory
- core_fraction = 20 - будет переменная vm_web_core_fraction
- preemptible = true - будет переменная vm_web_preemptible
- image_id в boot_disk.initialize_params - здесь использую data.yandex_compute_image.ubuntu.family через переменную vm_web_image_family

Добавляю в `variables.tf`:
```
variable "vm_web_name" {
  type    = string
  default = "netology-develop-platform-web"
}

variable "vm_web_platform_id" {
  type    = string
  default = "standard-v3"
}

variable "vm_web_cores" {
  type    = number
  default = 2
}

variable "vm_web_memory" {
  type    = number
  default = 1
}

variable "vm_web_core_fraction" {
  type    = number
  default = 20
}

variable "vm_web_preemptible" {
  type    = bool
  default = true
}

variable "vm_web_image_family" {
  type    = string
  default = "ubuntu-2004-lts"
}
```

Меняю `yandex_compute_image` и `yandex_compute_instance` в main.tf:
```
data "yandex_compute_image" "ubuntu" {
  family = var.vm_web_image_family
}

resource "yandex_compute_instance" "platform" {
  name        = var.vm_web_name
  platform_id = var.vm_web_platform_id

  resources {
    cores         = var.vm_web_cores
    memory        = var.vm_web_memory
    core_fraction = var.vm_web_core_fraction
  }

  boot_disk {
    initialize_params {
      image_id = data.yandex_compute_image.ubuntu.id
    }
  }

  scheduling_policy {
    preemptible = var.vm_web_preemptible
  }

  network_interface {
    subnet_id = yandex_vpc_subnet.develop.id
    nat       = true
  }

  metadata = {
    serial-port-enable = 1
    ssh-keys           = "ubuntu:${var.vms_ssh_public_root_key}"
  }
}
```

В результате `terraform plan` отрабатывает корректно, `terraform apply` создаёт такую же ВМ:

<img width="1932" height="411" alt="image" src="https://github.com/user-attachments/assets/c9eb20bb-609a-4f12-81f2-0ff266733481" />



### Задание 3

Изменения применены, разворачивается две ВМ в двух зонах доступности:

<img width="1945" height="440" alt="image" src="https://github.com/user-attachments/assets/0852f705-1398-4359-83ed-7be70c344bfa" />

Текущий main.tf:
```
resource "yandex_vpc_network" "develop" {
  name = var.vpc_name
}

# Первая подсеть для первой ВМ
resource "yandex_vpc_subnet" "develop" {
  name           = var.vpc_name
  zone           = var.default_zone
  network_id     = yandex_vpc_network.develop.id
  v4_cidr_blocks = var.default_cidr
}

# Вторая подсеть для второй ВМ
resource "yandex_vpc_subnet" "develop_b" {
  name           = "${var.vpc_name}-b"
  zone           = var.vm_db_zone
  network_id     = yandex_vpc_network.develop.id
  v4_cidr_blocks = var.vm_db_cidr
}

# Первая ВМ
data "yandex_compute_image" "ubuntu" {
  family = var.vm_web_image_family
}

resource "yandex_compute_instance" "platform" {
  name        = var.vm_web_name
  platform_id = var.vm_web_platform_id

  resources {
    cores         = var.vm_web_cores
    memory        = var.vm_web_memory
    core_fraction = var.vm_web_core_fraction
  }

  boot_disk {
    initialize_params {
      image_id = data.yandex_compute_image.ubuntu.id
    }
  }

  scheduling_policy {
    preemptible = var.vm_web_preemptible
  }

  network_interface {
    subnet_id = yandex_vpc_subnet.develop.id
    nat       = true
  }

  metadata = {
    serial-port-enable = 1
    ssh-keys           = "ubuntu:${var.vms_ssh_public_root_key}"
  }
}

# Вторая ВМ
data "yandex_compute_image" "ubuntu_db" {
  family = var.vm_db_image_family
}

resource "yandex_compute_instance" "platform_db" {
  name        = var.vm_db_name
  platform_id = var.vm_db_platform_id

  resources {
    cores         = var.vm_db_cores
    memory        = var.vm_db_memory
    core_fraction = var.vm_db_core_fraction
  }

  boot_disk {
    initialize_params {
      image_id = data.yandex_compute_image.ubuntu_db.id
    }
  }

  scheduling_policy {
    preemptible = var.vm_db_preemptible
  }

  network_interface {
    subnet_id = yandex_vpc_subnet.develop_b.id
    nat       = true
  }

  zone = var.vm_db_zone

  metadata = {
    serial-port-enable = 1
    ssh-keys           = "ubuntu:${var.vms_ssh_public_root_key}"
  }
}
```

Текущий vms_platform.tf:
```
# Переменные первой ВМ

variable "vm_web_name" {
  type    = string
  default = "netology-develop-platform-web"
}

variable "vm_web_platform_id" {
  type    = string
  default = "standard-v3"
}

variable "vm_web_cores" {
  type    = number
  default = 2
}

variable "vm_web_memory" {
  type    = number
  default = 1
}

variable "vm_web_core_fraction" {
  type    = number
  default = 20
}

variable "vm_web_preemptible" {
  type    = bool
  default = true
}

variable "vm_web_image_family" {
  type    = string
  default = "ubuntu-2004-lts"
}

# Переменные второй ВМ

variable "vm_db_name" {
  type    = string
  default = "netology-develop-platform-db"
}

variable "vm_db_platform_id" {
  type    = string
  default = "standard-v3"
}

variable "vm_db_cores" {
  type    = number
  default = 2
}

variable "vm_db_memory" {
  type    = number
  default = 2
}

variable "vm_db_core_fraction" {
  type    = number
  default = 20
}

variable "vm_db_preemptible" {
  type    = bool
  default = true
}

variable "vm_db_image_family" {
  type    = string
  default = "ubuntu-2004-lts"
}

variable "vm_db_zone" {
  type    = string
  default = "ru-central1-b"
}

variable "vm_db_cidr" {
  type    = list(string)
  default = ["10.0.2.0/24"]
}
```


### Задание 4

Содержимое outputs.tf:
```
output "vms_info" {
  description = "Созданные ВМ - результат:"
  value = [
    {
      instance_name = yandex_compute_instance.platform.name
      external_ip   = yandex_compute_instance.platform.network_interface[0].nat_ip_address
      fqdn          = yandex_compute_instance.platform.fqdn
    },
    {
      instance_name = yandex_compute_instance.platform_db.name
      external_ip   = yandex_compute_instance.platform_db.network_interface[0].nat_ip_address
      fqdn          = yandex_compute_instance.platform_db.fqdn
    }
  ]
}
```

Вывод `terraform output`:

<img width="1730" height="361" alt="image" src="https://github.com/user-attachments/assets/540a0985-4a9e-4a03-866b-397785b0ba91" />



### Задание 5

Поскольку задача использовать имменно несколько переменных - можно задвть в variables.tf переменные, из которых будем собирать имя:
```
variable "environment" {
  type    = string
  default = "env"
}

variable "role_web" {
  type    = string
  default = "web"
}

variable "role_db" {
  type    = string
  default = "db"
}
```
После этого в locals.tf задать блок с именами:
```
locals {
  vm_web_full_name = "${var.vpc_name}-${var.environment}-platform-${var.role_web}"
  vm_db_full_name  = "${var.vpc_name}-${var.environment}-platform-${var.role_db}"
}
```
А затем в main.tf прописать в name виртуальных машин поля из locals:
- В блоке `resource "yandex_compute_instance" "platform"`:
```
name = local.vm_web_full_name
```
- В блоке `resource "yandex_compute_instance" "platform_db"`:
```
name = local.vm_db_full_name
```

В итоге получаем имена ВМ в аутпуте:

<img width="1773" height="397" alt="image" src="https://github.com/user-attachments/assets/0b106096-4b8c-45aa-be2f-2e0364c76508" />




### Задание 6

Создаем `terraform.tfvars`:
```
# конфигурации ресурсов ВМ
vms_resources = {
  web = {
    cores         = 2
    memory        = 1
    core_fraction = 20
    hdd_size      = 5
    hdd_type      = "network-hdd"
    preemptible   = true
  }

  db = {
    cores         = 2
    memory        = 2
    core_fraction = 20
    hdd_size      = 5
    hdd_type      = "network-hdd"
    preemptible   = true
  }
}
```
Добавляем метадату в personal.auto.tfvars, чтобы не светить лишнего в коде
```
# метадата для ВМ
vms_metadata = {
  serial-port-enable = 1
  ssh-keys = "ubuntu:ssh-ed25519 AAAAневажно wiqtor@home-server"
}
```

Добавляем в `vms_platform.tf` блок переменных для ресурсов и метадаты:
```
# переменная для ресурсов ВМ
variable "vms_resources" {
  description = "Ресурсы для ВМ"
  type = map(object({
    cores          = number
    memory         = number
    core_fraction  = number
    hdd_size       = number
    hdd_type       = string
    preemptible    = bool
  }))
}

# переменная для metadata
variable "vms_metadata" {
  description = "метадата для ВМ"
  type        = map(string)
}
```
После этого там же в vms_platform нужно закоммеентировать эти переменные - они больше не нужны:
```
variable "vm_web_cores"
variable "vm_web_memory"
variable "vm_web_core_fraction"
variable "vm_web_preemptible"
variable "vm_db_cores"
variable "vm_db_memory"
variable "vm_db_core_fraction"
variable "vm_db_preemptible"
```

Затем нужно поменять блоки ресурсов в main.tf, чтобы он брал новые переменные:
Для первой ВМ platform:
```
resources {
  cores         = var.vms_resources["web"].cores
  memory        = var.vms_resources["web"].memory
  core_fraction = var.vms_resources["web"].core_fraction
}

boot_disk {
  initialize_params {
    image_id = data.yandex_compute_image.ubuntu.id
    size     = var.vms_resources["web"].hdd_size
    type     = var.vms_resources["web"].hdd_type
  }
}

scheduling_policy {
  preemptible = var.vms_resources["web"].preemptible
}

metadata = var.vms_metadata
```
Для второй ВМ platform_db:
```
resources {
  cores         = var.vms_resources["db"].cores
  memory        = var.vms_resources["db"].memory
  core_fraction = var.vms_resources["db"].core_fraction
}

boot_disk {
  initialize_params {
    image_id = data.yandex_compute_image.ubuntu_db.id
    size     = var.vms_resources["db"].hdd_size
    type     = var.vms_resources["db"].hdd_type
  }
}

scheduling_policy {
  preemptible = var.vms_resources["db"].preemptible
}

metadata = var.vms_metadata
```
Проверяем, что всё правильно через `terraform plan` - всё сходится, изменений нет:

<img width="1727" height="349" alt="image" src="https://github.com/user-attachments/assets/c55a82b3-f425-4817-ae7c-7506b01d4224" />



------

## Дополнительное задание (со звёздочкой*)
------
### Задание 7*

1. Напишите, какой командой можно отобразить **второй** элемент списка test_list. `local.test_list[1]`
2. Найдите длину списка test_list с помощью функции length(<имя переменной>). `length(local.test_list)`
3. Напишите, какой командой можно отобразить значение ключа admin из map test_map. ``
4. Напишите interpolation-выражение, результатом которого будет: "John is admin for production server based on OS ubuntu-20-04 with X vcpu, Y ram and Z virtual disks", используйте данные из переменных test_list, test_map, servers и функцию length() для подстановки значений.
`"${local.test_map["admin"]} is admin for ${local.test_list[2]} server based on OS ${local.servers.production.image} with ${local.servers.production.cpu} vcpu, ${local.servers.production.ram} ram and ${length(local.servers.production.disks)} virtual disks"`

Результат с сервера:

<img width="1935" height="278" alt="image" src="https://github.com/user-attachments/assets/1f3233bc-3b29-4e86-b50a-068863f14d5f" />


------

### Задание 8*
1. Напишите и проверьте переменную test и полное описание ее type в соответствии со значением из terraform.tfvars:
```
test = [
  {
    "dev1" = [
      "ssh -o 'StrictHostKeyChecking=no' ubuntu@62.84.124.117",
      "10.0.1.7",
    ]
  },
  {
    "dev2" = [
      "ssh -o 'StrictHostKeyChecking=no' ubuntu@84.252.140.88",
      "10.0.2.29",
    ]
  },
  {
    "prod1" = [
      "ssh -o 'StrictHostKeyChecking=no' ubuntu@51.250.2.101",
      "10.0.1.30",
    ]
  },
]
```
2. Напишите выражение в terraform console, которое позволит вычленить строку "ssh -o 'StrictHostKeyChecking=no' ubuntu@62.84.124.117" из этой переменной.

Добавляем указанное значение в terraform.tfvars.
После этого ззадаем переменную в variables.tf
```
variable "test" {
  description = "тестовая переменная для получения строки SSH подключения"
  type = list(
    map(
      list(string)
    )
  )
}
```
Теперь можно использовать выражение в terraform console для получения нужного результата:

<img width="1695" height="125" alt="image" src="https://github.com/user-attachments/assets/156cdf84-82f9-4d5e-a85e-280730ce07f2" />


------

### Задание 9*

Используя инструкцию https://cloud.yandex.ru/ru/docs/vpc/operations/create-nat-gateway#tf_1, настройте для ваших ВМ nat_gateway. Для проверки уберите внешний IP адрес (nat=false) у ваших ВМ и проверьте доступ в интернет с ВМ, подключившись к ней через serial console. Для подключения предварительно через ssh измените пароль пользователя: ```sudo passwd ubuntu```

### Правила приёма работыДля подключения предварительно через ssh измените пароль пользователя: sudo passwd ubuntu
В качестве результата прикрепите ссылку на MD файл с описанием выполненой работы в вашем репозитории. Так же в репозитории должен присутсвовать ваш финальный код проекта.

**Важно. Удалите все созданные ресурсы**.


### Критерии оценки

Зачёт ставится, если:

* выполнены все задания,
* ответы даны в развёрнутой форме,
* приложены соответствующие скриншоты и файлы проекта,
* в выполненных заданиях нет противоречий и нарушения логики.

На доработку работу отправят, если:

* задание выполнено частично или не выполнено вообще,
* в логике выполнения заданий есть противоречия и существенные недостатки. 

